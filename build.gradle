plugins {
    id 'application'
    id 'java'

    id 'com.gradle.build-scan' version '3.0'
    id "org.sonarqube" version "2.8"

    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'findbugs'
}

repositories {
    jcenter()
}

dependencies {
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

group       = 'pipeline.setup'
version     = '0.0.1-SNAPSHOT'
description = 'Learning how to create a CI pipeline using TravisCI and either SonarCloud.io or Coveralls.io'

wrapper {
    gradleVersion = '5.6.3'
    distributionType = Wrapper.DistributionType.ALL
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'BUNDLE'

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
            limit {
                counter = 'CLASS'
                value = 'MISSEDCOUNT'
                maximum = 0
            }
        }
    }
}

checkstyle {
    toolVersion = '8.26'
    ignoreFailures = 'true'
}

pmd {
    toolVersion = '6.3.0'
    sourceSets = [sourceSets.main]
    ignoreFailures = 'true'
}

findbugs {
    toolVersion = '3.0.1'
    sourceSets = [sourceSets.main]
    effort = 'max'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding    = 'UTF-8'
}

application {
    mainClassName = 'pipeline.setup.App.class'
}
